* CA3080 Operational Transconductance Amplifier
*
* SPICE (Simulation Program with Integrated Circuit Emphasis)
* SUBCIRCUIT
*
* Connections:
* INVERTING INPUT
* | NON-INVERTING INPUT
* | | NEGATIVE POWER SUPPLY
* | | | I BIAS
* | | | | OUTPUT
* | | | | | POSITIVE POWER SUPPLY
* | | | | | |
.SUBCKT CA3080 2 3 4 5 6 7
Q1 8 2 1 QN_OTA
Q2 8 3 12 QN_OTA
Q3 4 5 8 QN_OTA
Q4 7 9 1 QP_OTA
Q5 10 1 11 QP_OTA
Q6 9 10 11 QP_OTA
Q7 7 14 12 QP_OTA
Q8 13 12 6 QP_OTA
Q9 14 13 6 QP_OTA
Q10 15 11 6 QN_OTA
Q11 4 15 11 QN_OTA
D1 5 4 D_OTA
D2 1 10 D_OTA
D3 7 9 D_OTA
D4 12 13 D_OTA
D5 7 14 D_OTA
D6 15 4 D_OTA
*
.MODEL QN_OTA NPN(IS=21.48F XTI=3 EG=1.11 VAF=80 BF=550 ISE=50F NE=1.5
+ IKF=10M XTB=1.5 BR=.1 ISC=10F NC=2 IKR=3M RC=10 CJC=800F
+ MJC=.3333 VJC=.75 FC=.5 CJE=1.3P MJE=.3333 VJE=.75 TR=30N
+ TF=400P ITF=30M XTF=1 VTF=10 CJS=5.8P MJS=.3333 VJS=.75)
*
.MODEL QP_OTA PNP(IS=50F XTI=3 EG=1.11 VAF=80 BF=100 ISE=130F NE=1.5 IKF=1M
+ XTB=1.5 BR=1 ISC=0 NC=2 IKR=0 RC=0 CJC=4P MJC=.3333
+ VJC=.75 FC=.5 CJE=1.4P MJE=.3333 VJE=.75 TR=500N TF=23N ITF=.1
+ XTF=1 VTF=10 CJS=5.5P MJS=.3333 VJS=.75)
*
.MODEL D_OTA D(IS=2P RS=5 BV=40 CJO=3P TT=6N)
*
.ENDS

**************************************************
EQUIVALENT SUBSTITUTE FOR CA3080 IS ECG/NTE996
(SAME 8-PIN DIP PINOUT)
**************************************************

* ECG996 Operational Transconductance Amplifier
*
* SPICE (Simulation Program with Integrated Circuit Emphasis)
* SUBCIRCUIT
*
* Connections:
* INVERTING INPUT
* | NON-INVERTING INPUT
* | | NEGATIVE POWER SUPPLY
* | | | I BIAS
* | | | | OUTPUT
* | | | | | POSITIVE POWER SUPPLY
* | | | | | |
.SUBCKT ECG996 2 3 4 5 6 7
Q1 4 5 8_7 QN_OTA
Q2 8_7 2 9 QN_OTA
Q3 8_7 3 11 QN_OTA
Q4 7 8_6 9 QP_OTA
Q5 8_6 9 10 QP_OTA
Q6 7 12 11 QP_OTA
Q7 12 11 6 QP_OTA
Q8 13 10 6 QN_OTA
Q9 4 13 10 QN_OTA
D1 5 4 D_OTA
D2 7 8_6 D_OTA
D3 7 12 D_OTA
D4 13 4 D_OTA
*
.MODEL QN_OTA NPN(IS=21.48F XTI=3 EG=1.11 VAF=80 BF=550 ISE=50F NE=1.5
+ IKF=10M XTB=1.5 BR=.1 ISC=10F NC=2 IKR=3M RC=10 CJC=800F
+ MJC=.3333 VJC=.75 FC=.5 CJE=1.3P MJE=.3333 VJE=.75 TR=30N
+ TF=400P ITF=30M XTF=1 VTF=10 CJS=5.8P MJS=.3333 VJS=.75)
*
.MODEL QP_OTA PNP(IS=50F XTI=3 EG=1.11 VAF=80 BF=100 ISE=130F NE=1.5 IKF=1M
+ XTB=1.5 BR=1 ISC=0 NC=2 IKR=0 RC=0 CJC=4P MJC=.3333
+ VJC=.75 FC=.5 CJE=1.4P MJE=.3333 VJE=.75 TR=500N TF=23N ITF=.1
+ XTF=1 VTF=10 CJS=5.5P MJS=.3333 VJS=.75)
*
.MODEL D_OTA D(IS=2P RS=5 BV=40 CJO=3P TT=6N)
*
.ENDS

**************************************************
BELOW IS 1/2 LM13700
EQUIVALENT SUBSTITUTE FOR LM13700 IS NE5517/NTE870
(SAME 14-PIN DIP PINOUT)
**************************************************

* LM13700 Dual Operational Transconductance Amplifier
*
* SPICE (Simulation Program with Integrated Circuit Emphasis)
* SUBCIRCUIT
*
* Amplifier Bias Input
* | Diode Bias
* | | Positive Input
* | | | Negative Input
* | | | | Output
* | | | | | V-
* | | | | | | Buffer Input
* | | | | | | | Buffer Output
* | | | | | | | | V+
* | | | | | | | | |
.SUBCKT LM13700N 1 2 3 4 5 6 7 8 11
*
C1 6 4 4.8P
C2 3 6 4.8P
* Output capacitor
C3 5 6 6.26P
D1 2 4 DX
D2 2 3 DX
D3 11 21 DX
D4 21 22 DX
D5 1 26 DX
D6 26 27 DX
D7 5 29 DX
D8 28 5 DX
D10 31 25 DX
* Clamp for -CMR
D11 28 25 DX
* Ios source
F1 4 3 POLY(1) V6 1E-10 5.129E-2 -1.189E4 1.123E9
F2 11 5 V2 1.022
F3 25 6 V3 1.0
F4 5 6 V1 1.022
* Output impedance
F5 5 0 POLY(2) V3 V7 0 0 0 0 1
G1 0 33 5 0 .55E-3
I1 11 6 300U
Q1 24 32 31 QX1
Q2 23 3 31 QX2
Q3 11 7 30 QZ
Q4 11 30 8 QY
V1 22 24 0V
V2 22 23 0V
V3 27 6 0V
V4 11 29 1.4
V5 28 6 1.2
V6 4 32 0V
V7 33 0 0V
.MODEL QX1 NPN (IS=5E-16 BF=200 NE=1.15 ISE=.63E-16 IKF=1E-2)
.MODEL QX2 NPN (IS=5.125E-16 BF=200 NE=1.15 ISE=.63E-16 IKF=1E-2)
.MODEL QY NPN (IS=6E-15 BF=50)
.MODEL QZ NPN (IS=5E-16 BF=266)
.MODEL DX D (IS=5E-16)
.ENDS

**************************************************